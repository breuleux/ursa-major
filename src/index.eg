
require:
   util -> inspect
   kaiser/reg

provide:
   INFO, CONTENTS
   define-struct
   struct


if global["@@URSAMAJOR"]:
   return global["@@URSAMAJOR"]


INFO = Symbol{.STRUCT-INFO}
CONTENTS = Symbol{.STRUCT-CONTENTS}
VALIDATED = Symbol{.VALIDATED}

proto-base = {
   method to-string{} =
      info = @[INFO]
      '{info.name or "<struct>"}\{{values.join{", "}}\}' where
         values = info.field-names each field -> '{field} = {repr}' where
            repr = match @[field]:
               String? s -> inspect{s}
               x -> String{x}
   method validate{} =
      true
   method [^Symbol.equals]{x} =
      if Object.get-prototype-of{x} !== Object.get-prototype-of{@}:
         return false
      @[INFO].field-names each field ->
         if @[field] != x[field]:
            return false
      return true
}

define-struct{spec} =
   spec as {
      => name
      => mixins = {}
   }

   proto = Object.create{proto-base}

   mfields = {=}
   mmethods = {=}
   accmethods = {
      ;; constructor = {true, {}}
      validate = {false, {}}
   }

   contribute-accmethods{m} =
      items{accmethods} each {name, {match require-own, acc}} ->
         true? ->
            if Object.has-own-property{m, name}:
               acc.push{m[name]}
         else ->
            if m[name]:
               acc.push{m[name]}

   mixins each mixin ->
      var m = mixin
      if m.prototype:
         m = m.prototype
      contribute-accmethods{m}
      if m[INFO] as info:
         mfields &: info.fields
         mmethods &: info.methods
      else:
         while true:
            mmethods &: m
            m = Object.get-prototype-of{m}
            if not m or m === Object.prototype:
               break

   fields = mfields & [spec.fields or {=}]

   contribute-accmethods{spec.methods}
   methods = mmethods & [spec.methods or {=}]
   methods &:
      object with items{accmethods} each {name, {_, match parts}} ->
         {} ->
            continue
         {f} ->
            {name, f}
         else ->
            method aggregated{*args} =
               var rval = null
               parts each part -> rval = part.apply{@, args}
               rval
            {name, aggregated}

   proto[INFO] = {
      = name
      = fields
      = methods
      field-names = keys{fields}
      method-names = keys{methods}
   }

   _validate{@} =
      @[VALIDATED] = true
      try:
         @validate??{}
      catch e:
         @[VALIDATED] = false
         throw e

   construct{values = {=}} =
      rval = Object.create{proto}
      rval[CONTENTS] = {=}
      if methods.has-own-property{.constructor}:
         methods.constructor.apply{rval, arguments}
      else:
         items{fields} each {name, settings} ->
            default = if{Object.has-property.call{settings, .default}
                         settings.default
                         null}
            v = if{Object.has-property.call{values, name}, values[name], default}
            rval[name] = v
      _validate{rval}
      rval

   items{fields} each {name, settings} ->
      Object.define-property{proto, name} with {
         method get{} =
            if not @[VALIDATED]:
               _validate{@}
            @[CONTENTS][name]
         method set{value} =
            @[CONTENTS][name] =
               if settings.validate:
                  try:
                     settings.validate{value}
                  catch err:
                     throw E.struct{'''Error setting '{name}': {err.message}''', err}
               else:
                  value
            @[VALIDATED] = false
      }

   items{methods} each {name, m} ->
      proto[name] = m

   if spec.type-id:
      reg.register{proto} with {
         type-id = spec.type-id
         serialize{@} =
            @[CONTENTS]
         deserialize{contents} =
            construct{contents}
      }

   construct.prototype = proto
   construct


macro{define-struct} struct{`{^head, ^body}`} =

   {name, mixins} =
      match head:
         `[^name < {^*mixins}]` -> {name, #data{*mixins}}
         `[^name < ^mixins]` -> {name, #data{mixins}}
         name -> {name, #data{}}

   #symbol{sname} or #value{sname} or sname is null = name
   #multi! #multi{*stmts} = body

   dfs = @deps["define-struct"]

   fields = #data{}
   methods = `methods`
   results = #splice{
      `methods = {=}`
      `var type-id = null`
   }

   stmts each match stmt ->
      `type-id: ^type-id` or `id: ^type-id` ->
         results.push with `type-id = ^type-id`
      `^lhs = ^rhs` or lhs and rhs is null ->
         pc = @PatternCompiler{lhs, @, opt} where opt =
            @pattern_handlers.build_object & {
               assign{#symbol{@camelCase! v} or #value{v}, value} =
                  #do{#assign{`[^methods][^=v]`, value}}
               declare{vars} = {}
               wrap_target{match} =
                  `^args -> ^body` ->
                     {env => other_env} = body
                     it = #symbol{"@"} & {env = other_env}
                     it2 = #symbol{"self"} & {env = other_env}
                     `_lambda{
                        ^args
                        splice: [let ^it = this, let ^it2 = this]
                        ^body
                        ^=null
                        ^=false
                     }`
                  other ->
                     other
               success{x} =
                  #multi{}
            }
         pc.compile{}

         compile-description{match x} =
            #macro{m} ->
               #send{#macro{mm}, #void{}} where mm{ctx, info, form, arg} =
                  try:
                     m.call{info, ctx, info, form, arg}
                  catch e:
                     `E.type-specification{"Could not generate type specification."}`
            #assign{#symbol{s} or #value{s}} ->
               #data{#value{.assign}, #value{s}}
            #send{#symbol{.get-checker}, #data{x}} ->
               x
            {operation in {"check", "project"
                           "any", "all"
                           "maybe"
                           "send", "symbol", "data"
                           "multi", "void"
               }, *contents} ->
               #data{#value{operation}, *cs} where cs =
                  contents each c -> compile-description{c}
            String? or Boolean? or null? or undefined? ->
               #value{x}
            else ->
               #value{null}

         match pc:
            {arguments => {}, vars => match} ->
               {v} and match is rhs ->
                  do:
                     tr = `validate{x} = [^lhs = x, ^v]`
                     tp = `type{} = ^compile-description{pc.compiled}`
                  null? ->
                     fields.push with `^v = {^tr, ^tp}`
                  else ->
                     fields.push with `^v = {^tr, ^tp, default = ^rhs}`
               else ->
                  throw E.syntax.struct with
                     '''Pattern '{@gettext{lhs}}' should define exactly one variable.'''
                     node = lhs
            {=> arguments} when rhs == null ->
               throw E.syntax.struct with
                  '''Missing method body for: '{@gettext{lhs}}' '''
                  node = lhs
            {=> arguments} ->
               results.push with pc.extract_from_rhs{rhs}

   results.push with
      ```
      ^name = [^dfs]{
          name = ^=sname
          fields = ^fields
          methods = methods
          mixins = ^mixins
          type-id = type-id
      }
      ```

      ;; if super:
      ;;    then: `^name = ___extend{[^dfs]{^=sname, ^fields, methods, options}, ^super}`
      ;;    else: `^name = [^dfs]{^=sname, ^fields, methods, options}`
   results


global["@@URSAMAJOR"] = module.exports
